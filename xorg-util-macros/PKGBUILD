# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=xorg-util-macros
pkgver=1.20.1
pkgrel=1
pkgdesc='X.Org Autotools macros'
arch=('any')
groups=('development')
license=('custom')
provides=('util-macros')
replaces=('util-macros')
conflicts=('util-macros')
url="https://xorg.freedesktop.org/"
msys2_references=(
  "anitya: 15037"
)
depends=()
makedepends=('autotools')
source=("https://xorg.freedesktop.org/releases/individual/util/util-macros-${pkgver}.tar.xz")
sha256sums=('0b308f62dce78ac0f4d9de6888234bf170f276b64ac7c96e99779bb4319bcef5')

prepare() {
  cd "${srcdir}/util-macros-${pkgver}"
  autoreconf -fiv
}

build() {
  cd "${srcdir}/util-macros-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/util-macros-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/util-macros-${pkgver}"
  make install DESTDIR="${pkgdir}"

  install -m755 -d "${pkgdir}/usr/lib"
  mv "${pkgdir}/usr/share/pkgconfig" "${pkgdir}/usr/lib/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
